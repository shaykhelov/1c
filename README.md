# Программное добавление элементов на управляемую форму
### Добавление команды
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Добавление команды формы
	Команда = Команды.Добавить("ПоказатьПредупреждение");
	Команда.Заголовок = "Показать предупреждение";
	Команда.Действие  = "КомандаПоказатьПредупреждение";
	
	//Добавление кнопки формы
	КнопкаФормы = Элементы.Добавить(
		"КнопкаПоказатьПредупреждение",
		Тип("КнопкаФормы"),
		ЭтаФорма);
		
	КнопкаФормы.ИмяКоманды = "ПоказатьПредупреждение";
	КнопкаФормы.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПоказатьПредупреждение(Команда)
	
	ПоказатьПредупреждение(, "ОК!");
	
КонецПроцедуры
```

### Добавление поля ввода
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ПолеВвода = Элементы.Добавить(
		"Комментарий",    //Имя элемента формы
		Тип("ПолеФормы"), //Тип, всегда ПолеФормы
		ЭтаФорма);	      //Контейнер для поля ввода (Форма,Группа,Страница)
		
	ПолеВвода.Заголовок = "Комментарий";
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	
	//Установка связи элемента с данными
	ПолеВвода.ПутьКДанным = "Объект.Комментарий";
	
	//Можно добавить обработчик события
	//См. синтакс-помощник чтобы узнать список событий
	// Интерфейс (управляемый) -> Поле формы -> ПолеФормы -> События
	// а так же остальные варианты расширения поля формы в зависимости от вида кнопки
	// Интерфейс (управляемый) -> Поле формы -> Расширение поля ввода -> События
	// и прочее
	ПолеВвода.УстановитьДействие(
		"ПриИзменении", 		    //Имя события
		"ПриИзмененииКомментария"); //Имя процедуры обработчика
		
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииКомментария(Элемент)
	
	Сообщить("Поле ""Комментарий"" изменено!");
	
КонецПроцедуры
```

### Добавление реквизита формы
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Пример:
	//Добавление реквизита с именем "АдресДоставки", тип Строка
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ТипРеквизита = Новый ОписаниеТипов("Строка");
	
	РеквизитФормы = Новый РеквизитФормы(
		"АдресДоставки",  //Имя реквизита формы
		ТипРеквизита,     //Тип
		"",               //Путь  (Пусто, "Объект", ИмяТЧ)
						  // например "Объект.Товары"	
		"Адрес доставки");//Заголовок
						  							 
	ДобавляемыеРеквизиты.Добавить(РеквизитФормы);
	
	//Заставляем форму создать новые реквизиты
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	//Обращение к реквизиту формы из кода
	ЭтаФорма.АдресДоставки = "ул. Пушкина";
	
	//Вывод реквизита на форму (при необходимости)
	ПолеВвода = Элементы.Добавить("ФормаАдресДоставки", Тип("ПолеФормы"), ЭтаФорма);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "АдресДоставки";
	
КонецПроцедуры
```

### Добавление группы формы
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Пример:
	//Добавление группы формы
	//Добавление элементов в группу формы
	
	Группа = Элементы.Добавить(
		"ГруппаШапка",
		Тип("ГруппаФормы"),
		ЭтаФорма);
		
	Группа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	
	Группа.Заголовок = "Реквизиты объекта:";
	
	//Добавить поле ввода в группу
	ПолеВвода = Элементы.Добавить("ФормаКомментарий", Тип("ПолеФормы"), Группа);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Комментарий";

	
КонецПроцедуры
```

### Добавление страниц формы
```
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//Пример:
	//Добавление группы страниц
	//Добавление страниц
	//Добавление элементов на страницы
	
	СтраницыФормы = Элементы.Добавить(
		"СтраницыФормы",
		Тип("ГруппаФормы"),
		ЭтаФорма);
		
	СтраницыФормы.Вид = ВидГруппыФормы.Страницы;
	
	//Подключить обработчик при смене страницы
	СтраницыФормы.УстановитьДействие("ПриСменеСтраницы", "ДействиеПриСменеСтраницы");
	
	//Добавление 1-й страницы
	СтраницаФормы1 = Элементы.Добавить(
		"Страница1",
		Тип("ГруппаФормы"),
		СтраницыФормы);
		
	СтраницаФормы1.Вид = ВидГруппыФормы.Страница;
	СтраницаФормы1.Заголовок = "Первая страница";
	
	//Добавление 2-й страницы
	СтраницаФормы2 = Элементы.Добавить(
		"Страница2",
		Тип("ГруппаФормы"),
		СтраницыФормы);
		
	СтраницаФормы2.Вид = ВидГруппыФормы.Страница;
	СтраницаФормы2.Заголовок = "Вторая страница";
	
	//Добавить поле ввода на страницу 1
	ПолеВвода = Элементы.Добавить("ФормаКомментарий", Тип("ПолеФормы"), СтраницаФормы1);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Комментарий";
	
	//Добавить поле ввода на страницу 2
	ПолеВвода = Элементы.Добавить("ФормаКомментарий2", Тип("ПолеФормы"), СтраницаФормы2);
	ПолеВвода.Вид = ВидПоляФормы.ПолеВвода;
	ПолеВвода.ПутьКДанным = "Объект.Комментарий";
	
КонецПроцедуры

&НаКлиенте
Процедура ДействиеПриСменеСтраницы(Элемент)
	
	Сообщить("Изменена страница");
	
КонецПроцедуры
```


### Открытие объекта по ссылке
```
&После("ПриНачалеРаботыСистемы")
Процедура ПриНачалеРаботыСистемы()
	
	//Ссылка = "e1cib/data/Документ.УПН_СделкаПоНедвижимости?ref=923250465d74e35911ebbc6826d073d5";
	Ссылка = "e1cib/data/Справочник.Партнеры?ref=932150465d74e35911ebace20465110b";
	
	ПерейтиПоНавигационнойСсылке(Ссылка);
	
КонецПроцедуры
```


### Добавление/изменение значения дополнительного реквизита
```
ТаблицаСвойствИЗначений = Новый ТаблицаЗначений;
ТаблицаСвойствИЗначений.Колонки.Добавить("Свойство", Новый ОписаниеТипов("ПланВидовХарактеристикСсылка.ДополнительныеРеквизитыИСведения"));
ТаблицаСвойствИЗначений.Колонки.Добавить("Значение");

НоваяСтрока = ТаблицаСвойствИЗначений.Добавить();
НоваяСтрока.Свойство = ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ИмяСвойства");
НоваяСтрока.Значение = Значение;
```
